plugins {
    id "fabric-loom"
}

var target_version = project.path.replace(":", "")
version = "$mod_version+$target_version"
group = maven_group

base {
    archivesName = archives_name
}

repositories {
    maven { url "https://jitpack.io" }
}

loom {
    decompilers {
        vineflower {
            options.putAll(["mark-corresponding-synthetics": "1", "indent-string": "    "])
        }
    }
    mixin {
        useLegacyMixinAp = false
    }
}


dependencies {
    modImplementation "net.fabricmc:fabric-loader:$loader_version"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": version, "target_version": target_version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(8)
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

jar {
    from "LICENSE"
}
